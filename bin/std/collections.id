# Author: SplittyDev (github.com/splittydev)

interface IEnumerable {
    func __iter__ (self);
}

class Stack : IEnumerable {

    # Constructor
    # Optional arguments:
    # int capacity
    # iterable content
    # @param variadic args Optional arguments
    func Stack (self, **kwargs) {
        self.top = 0
        self.list = []
        self.capacity = -1
        foreach (key in kwargs) {
            given (key) {
                when "capacity" {
                    capacity = kwargs [key]
                    if (capacity isnot Int)
                        raise Exception ("Argument 'capacity' is expected to be an 'Int'.")
                    self.capacity = capacity
                }
            }
        }
    }

    # Pushes an item onto the stack
    # @param object item The item to be pushed onto the stack
    # @return null
    func push (self, item) {
        if (self.top == self.capacity)
            raise Exception ("Maximum capacity reached.")
        self.list.add (item)
        self.top += 1
    }

    # Pops an item from the stack
    # @return object The item that was popped from the stack
    func pop (self) {
        if (self.top == 0)
            raise Exception ("There are no elements on the stack.")
        self.top -= 1
        item = self.list [self.top]
        self.list.removeAt (self.top)
        return item
    }

    # Gets the length
    # @return int The length
    func __len__ (self) {
        return self.top
    }

    # Gets the string representation
    # @return The string representation
    func __str__ (self) {
        return __repr__ ()
    }

    # Gets the string representation
    # @return The string representation
    func __repr__ (self) {
        i = 0
        accum = StringBuffer ()
        accum.append ("[")
        foreach (item in self) {
            if (i > 0) accum.append (", ")
            accum.append (repr (item))
            i += 1
        }
        accum.append ("]")
        return Str (accum)
    }

    # Gets the item at the specified index
    # @param int i The index
    # @return object The item at the specified index
    func __getIndex__ (self, i) {
        return self.list [self.top - i - 1]
    }

    # Sets the item at the specified index
    # @param int i The index
    # @param object item The item
    # @return null
    func __setIndex__ (self, i, item) {
        self.list [self.top - i - 1] = item
    }
    
    # Gets the iterator
    # @return iterator The iterator
    func __iter__ (self) {
        for (i = 0; i < self.top; i += 1) {
            print ("i: " + Str (i))
            yield self [i]
        }
    }
}

class TypedStack : Stack {

    # Constructor
    # Optional arguments:
    # 0 -> capacity (int)
    # @param typedef T The type
    # @param variadic args Optional arguments
    func TypedStack (self, T, *args) {
        super (*args);
        self.type = T
    }

    # Pushes an item onto the stack
    # @param object item The item to be pushed onto the stack
    # @return null
    func push (self, item) {
        if (item isnot self.type)
            raise Exception ("Invalid type!")
        self.__super__.push (item)
    }
}